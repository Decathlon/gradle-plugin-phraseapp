// Top-level build file where you can add configuration options common to all sub-projects/modules.

allprojects {
    repositories {
        mavenCentral()
    }
}

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath(libs.sonarqube.gradle)
        classpath(libs.jacoco.gradle)
        classpath(libs.kotlin.gradle)
        classpath(libs.vanniktech.maven.publish.gradle)
    }
}

apply plugin: 'jacoco'
apply plugin: "org.sonarqube"

tasks.register("codeCoverageReport", JacocoReport) {
    subprojects { subproject ->
        subproject.plugins.withType(JacocoPlugin).configureEach {
            subproject.tasks.matching({ t -> t.extensions.findByType(JacocoTaskExtension) }).configureEach { testTask ->
                sourceSets subproject.sourceSets.main
                executionData(testTask)
            }

            subproject.tasks.matching({ t -> t.extensions.findByType(JacocoTaskExtension) }).forEach {
                rootProject.tasks.codeCoverageReport.dependsOn(it)
            }
        }
    }

    reports {
        xml.enabled true
        html.enabled true
    }
}

sonarqube {
    properties {
        property("sonar.projectKey", "Decathlon_gradle-plugin-phraseapp")
        property("sonar.organization", "decathlon")
        property("sonar.host.url", "https://sonarcloud.io")
        property("sonar.pullrequest.provider", "GitHub")
        property("sonar.pullrequest.github.repository", "Decathlon/gradle-plugin-phraseapp")
        property("sonar.coverage.jacoco.xmlReportPaths", "$rootDir/build/reports/jacoco/codeCoverageReport/codeCoverageReport.xml")
        property("sonar.login", System.getenv("SONAR_TOKEN"))
        property("sonar.debug", true)
    }
}
